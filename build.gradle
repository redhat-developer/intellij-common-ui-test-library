plugins {
    id 'java-library'
    id 'maven-publish'
    id 'org.jetbrains.kotlin.jvm' version '1.4.32'
    id 'io.github.gradle-nexus.publish-plugin' version '1.1.0'
    id "org.sonarqube" version "3.3"
}

group 'com.redhat.devtools.intellij'
version projectVersion

def remoteRobotVersion = '0.11.20'

repositories {
    mavenLocal()
    mavenCentral()
    jcenter()
    maven {
        url 'https://packages.jetbrains.team/maven/p/ij/intellij-dependencies'
    }
}

dependencies {
    compile 'org.jetbrains.kotlin:kotlin-stdlib:1.6.0'
    compile 'org.jetbrains.kotlin:kotlin-reflect:1.6.0'
    api 'commons-io:commons-io:2.4'
    api 'org.junit.jupiter:junit-jupiter-api:5.3.1'
    api 'org.junit.jupiter:junit-jupiter-engine:5.3.1'
    api "com.intellij.remoterobot:remote-robot:$remoteRobotVersion"
    api "com.intellij.remoterobot:remote-fixtures:$remoteRobotVersion"
}

java {
    withSourcesJar()
    withJavadocJar()
}

test {
    useJUnitPlatform()
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            pom {
                from components.java

                name = 'IntelliJ common UI test library'
                description = 'Common utilities for IntelliJ UI testing'
                url = 'https://github.com/redhat-developer/intellij-common-ui-test-library'

                licenses {
                    license {
                        name = 'Eclipse Public License 2.0'
                        url = 'https://www.eclipse.org/legal/epl-v20.html'
                    }
                }
                developers {
                    developer {
                        name = 'Red Hat Developer'
                        email = 'developers@redhat.com'
                    }
                }
                scm {
                    connection = 'scm:git:git://github.com/redhat-developer/intellij-common-ui-test-library.git'
                    developerConnection = 'scm:git:ssh://git@github.com:redhat-developer/intellij-common-ui-test-library.git'
                    url = 'https://github.com/redhat-developer/intellij-common-ui-test-library/'
                }
            }
        }
    }
    repositories {
        maven {
            def releasesRepoUrl = "https://repository.jboss.org/nexus/service/local/staging/deploy/maven2/"
            def snapshotsRepoUrl = "https://repository.jboss.org/nexus/content/repositories/snapshots/"
            url = version.endsWith('SNAPSHOT') ? snapshotsRepoUrl : releasesRepoUrl
            credentials {
                username = nexusUser
                password = nexusPassword
            }
        }
    }
}

nexusPublishing {
    packageGroup = 'JBoss Releases Staging Profile'
    repositories {
        jbossNexus {
            stagingProfileId = '2161b7b8da0080'
            username = nexusUser
            password = nexusPassword
            nexusUrl.set(uri('https://repository.jboss.org/nexus/service/local/'))
            snapshotRepositoryUrl.set(uri('https://repository.jboss.org/nexus/content/repositories/snapshots/'))
        }
    }
}

sonarqube {
    properties {
        property "sonar.projectKey", "redhat-developer_intellij-common-ui-test-library"
        property "sonar.organization", "redhat-developer"
        property "sonar.host.url", "https://sonarcloud.io"
        property "sonar.sources", "src"
    }
}
